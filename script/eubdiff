#!/usr/bin/perl

use strict;
use warnings;

use Fcntl qw(:mode);
use Getopt::Std;

my (%opt, %path2meta);

getopts('ndi', \%opt);
usage() if @ARGV != 1;

my ($arf) = @ARGV;
$arf =~ s/(\.eum)?$/.eum/;

# Read in prev archive meta
open my $arfh, '<', $arf or die "Can't open $arf: $!";
while (<$arfh>) {
    next if !m{^([-+])([fdlbcps?](?: [^\s/.:][0-9]+)+) (.+)$};
    ### next if !m{^([-+])([fdlbcps?] [^/.]+) ([./].+)$};
    my ($status, $meta, $path) = ($1, $2, $3);
    if ($status eq '+') {
        $path2meta{$path} = $meta;
    }
    elsif ($status eq '-') {
        delete $path2meta{$path};
    }
}

# Read in output of eubar -m
while (<STDIN>) {
    next if !m{^[+]([fdlbcps?](?: [^\s/.:][0-9]+)+) (.+)$};
    ### next if !m{^[+]([fdlbcps?] [^/.]+) ([./].+)$};
    my ($meta_now, $path) = ($1, $2);
    my $meta = $path2meta{$path};
    print('+', $meta_now, ' ', $path, "\n"), next if !defined $meta;
    next if $meta eq '';
    if ($opt{'n'}) {
        $meta = norm_meta($meta);
        $meta_now = norm_meta($meta_now);
    }
    $path2meta{$path} = '';
    print('+', $meta_now, ' ', $path, "\n") if $meta_now ne $meta;
}
# Delete anything we didn't see in the list of files on stdin
foreach my $path (sort keys %path2meta) {
    my $meta = $path2meta{$path};
    print '-', $meta, ' ', $path, "\n" if $meta ne '';
}

# --- Functions

sub typechar {
    my ($mode) = @_;
    return S_ISREG($mode)  ? 'f' :
           S_ISDIR($mode)  ? 'd' :
           S_ISLNK($mode)  ? 'l' :
           S_ISBLK($mode)  ? 'b' :
           S_ISCHR($mode)  ? 'c' :
           S_ISFIFO($mode) ? 'p' :
           S_ISSOCK($mode) ? 's' : '?';
}

sub norm_meta {
    local $_ = shift;
    s/^([fdlbcps?]) // or die;
    my $typechar = $1;
    my %meta = map { substr($_,0,1) => substr($_,1) } split / /;
    join(' ', $typechar, map { $_ . $meta{$_} } sort keys %meta);
}

sub stat2meta {
    my $meta = sprintf('%s c%ld', typechar($_[2]), $_[10]);
    $meta .= sprintf(' d%ld', $_[0]) if $opt{'d'};
    $meta .= sprintf(' g%ld', $_[5]);
    $meta .= sprintf(' i%ld', $_[1]) if $opt{'i'};
    $meta .= sprintf(' m%ld p%o r%ld u%ld', @_[9,2,6,4]);
    return $meta;
}

sub warning {
    print STDERR "eubdiff: ", @_, "\n";
}

